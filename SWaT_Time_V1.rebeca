//Tank1 Initial Status
env boolean tank1_low = true;
env boolean tank1_medium = false;
env boolean tank1_high = false;
//----------------------------------------------------
//Tank2 Initial Status
env boolean tank2_low = true;
env boolean tank2_medium = false;
env boolean tank2_high = false;
//----------------------------------------------------
//Tank3 Initial Status
env boolean tank3_low = true;
env boolean tank3_medium = false;
env boolean tank3_high = false;
//----------------------------------------------------



reactiveclass Tank1(5){
	knownrebecs{
		sensorTank1 sensor;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	Tank1(){
		underFlow = false;
		low = tank1_low; 
		medium = tank1_medium;
		high = tank1_high;
		overFlow = false;
	}
	msgsrv status(){	
		if (underFlow){
		sensor.reportStatus(0); 
		}else if (low){
		sensor.reportStatus(1);
		}else if (medium){
		sensor.reportStatus(2);
		}else if (high){
		sensor.reportStatus(3);
		}else if (overFlow){
		sensor.reportStatus(4);
		}
	}
	msgsrv waterIncrease(){	
		if (low) { 
			low = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			high = true;
		} else if (high) { 
			high = false;
			overFlow = true;
		}
		self.status();
	}
	msgsrv waterDecrease(){	
		if (high) { 
			high = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			low = true;
		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.status();
	}

}


reactiveclass sensorTank1(3){
	knownrebecs{
		Tank1	tank1;
		PLC1	plc1;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	sensorTank1(){
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false; 	
	}
	msgsrv sense(){	
		tank1.status();	
	}
	msgsrv reportStatus(int waterLevel) {
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false;	
		// value 0 means "underFlow", value 1 means "low", value 2 means "medium", vaule 3 means "high", and value 4 means "overFlow"
		if (waterLevel == 0) { 
			underFlow = true;
			plc1.processSensorData(0);
		}else if (waterLevel == 1) {
			low = true;
			plc1.processSensorData(1);
		}else if (waterLevel == 2) {
			medium = true;
			plc1.processSensorData(2);
		}else if (waterLevel == 3) {
			high = true;
			plc1.processSensorData(3);
		}else if (waterLevel == 4) {
			overFlow = true;
			plc1.processSensorData(4);
		}	
	}

}



reactiveclass PLC1(5){
	knownrebecs{
		Pump1 pump1;
		Valve valve;
		sensorTank1 sensor;
	}
	statevars{
		boolean openReqPlc2;
	}
	PLC1(){	 
		openReqPlc2 = false;	
	}
	msgsrv processSensorData(int waterLevel){
		if (waterLevel == 1) { 
			pump1.on();
			valve.close();
		}else if ((waterLevel == 3) && (openReqPlc2 == true)){ 
				openReqPlc2 = false;
				valve.open();
		}else if (waterLevel == 3) {
				pump1.off();
				valve.close();
		}else if ((waterLevel == 2) && (openReqPlc2 == true)) {
				openReqPlc2 = false;
				valve.open();
		}else if (waterLevel == 2){
				pump1.on();
				valve.close();
		} 		
	}
	msgsrv openReq(){
		openReqPlc2 = true;
		sensor.sense();
	}
	msgsrv closeReq(){
		valve.close();
	}

}

reactiveclass Pump1(3){
	knownrebecs{
		Tank1 tank1;
	}
	statevars{
		boolean on;
	}
	Pump1(){
	 	on = false;	
	}
	msgsrv on(){	
		on = true;
		tank1.waterIncrease();
	}
	msgsrv off(){
		on = false;	
	}

}



reactiveclass Valve(7){
	knownrebecs{
		Tank1 tank1;
		Tank2 tank2;
	}
	statevars{
		boolean open;
	}
	Valve(){
	 	open = false;	 	
	}
	msgsrv open(){	
		open = true;
		tank1.waterDecrease();
		tank2.waterIncrease();
	}
	msgsrv close(){
		open = false;	
	}

}



reactiveclass Tank2(3){
	knownrebecs{
		sensorTank2 sensor;
		reverseUnit unit;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	Tank2(){
		underFlow = false;
		low = tank2_low;
		medium = tank2_medium;
		high = tank2_high;
		overFlow = false;
	}
msgsrv status(){	
		if (underFlow){
			sensor.reportStatus(0); 
		}else if (low){
		sensor.reportStatus(1);
		}else if (medium){
			sensor.reportStatus(2);
			unit.waterIncreaseTank3(); // water flows to tank3
		}else if (high){
			sensor.reportStatus(3);
			unit.cleanWater(); // clean water flows out when tank2 water-level reaches high
		}else if (overFlow){
			sensor.reportStatus(4);
		}
	}
	msgsrv waterIncrease(){	
		if (low) { 
			low = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			high = true;
		} else if (high) { 
			high = false;
			overFlow = true;
		}
		self.status();
	}
	msgsrv waterDecrease(){	
		if (high) { 
			high = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			low = true;
		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.status();
	}

}


reactiveclass sensorTank2(3){
	knownrebecs{
		Tank2	tank2;
		PLC2	plc2;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	sensorTank2(){
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false; 
		self.sense();	
	}
	msgsrv sense(){	
		tank2.status();	
	}
	msgsrv reportStatus(int waterLevel) {	
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false; 
		if (waterLevel == 0) {
			underFlow = true;
			plc2.processSensorData(0);
		}else if (waterLevel == 1) {
			low = true;
			plc2.processSensorData(1);
		}else if (waterLevel == 2) {
			low = true;
			plc2.processSensorData(2);
		}else if (waterLevel == 3) {
			high = true;
			plc2.processSensorData(3);
		}else if (waterLevel == 4) {
			overFlow = true;
			plc2.processSensorData(4);
		}	
	}

}



reactiveclass PLC2(5){
	knownrebecs{
		PLC1 plc1;
		PLC3 plc3;
		sensorTank2 sensor;
	}
	statevars{
		boolean plcAlarm;
	}
	PLC2(){	 
		plcAlarm = false;
	}
	msgsrv processSensorData(int waterLevel){
		if (waterLevel == 0) { 
			plcAlarm = true;
		}else if (waterLevel == 3){ 
			plc1.closeReq();
		}else if (waterLevel == 2) { 
			delay(1);
			plc3.onReq();  //assumption is that when water-level-tank2 = medium ---> onReq to plc3
		}else if (waterLevel == 1) { 
			plc1.openReq(); //assumption is that when water-level-tank2 = low ---> openReq to plc1
		} 
	}

}


reactiveclass reverseUnit(5){
	knownrebecs{
		Tank2 tank2;
		Tank3 tank3;
	}
	msgsrv waterIncreaseTank3(){
		tank3.waterIncrease();
	}
	msgsrv cleanWater(){
		tank2.waterDecrease() after(2);
	}
}

reactiveclass Tank3(5){
	knownrebecs{
		sensorTank3 sensor;
		Tank2 tank2;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	Tank3(){
		underFlow = false;
		low = tank3_low;
		medium = tank3_medium;
		high = tank3_high;
		overFlow = false; 	
	}
	msgsrv status(){	
		if (underFlow){
		sensor.reportStatus(0); // 1 means status= low, 2 means status= medium, ...
		}else if (low){
		sensor.reportStatus(1);
		}else if (medium){
		sensor.reportStatus(2);
		}else if (high){
		sensor.reportStatus(3);
		}else if (overFlow){
		sensor.reportStatus(4);
		}
	}
	msgsrv waterIncrease(){	
		if (low) { 
			low = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			high = true;
		} else if (high) { 
			high = false;
			overFlow = true;
		}
		self.status();
	}
	msgsrv waterDecrease(){	
		if (high) { 
			high = false;
			medium = true;
		} else if (medium) { 
			medium = false;
			low = true;
		} else if (low) { 
			low = false;
			underFlow = true;
		}
		self.status();
	}
	msgsrv waterWholeDeplete(){	
		underFlow = false;
		low = true;
		medium = false;
		high = false;
		overFlow = false;
		self.status(); 
		delay(1);
		tank2.waterIncrease();
	}
	
}

reactiveclass sensorTank3(3){
	knownrebecs{
		Tank3	tank3;
		PLC3	plc3;
	}
	statevars{
		boolean underFlow,low,medium,high,overFlow;
	}
	sensorTank3(){
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false; 
	}
	msgsrv sense(){	
		tank3.status();	
	}
	msgsrv reportStatus(int waterLevel) {	
		underFlow = false;
		low = false;
		medium = false;
		high = false;
		overFlow = false; 
		if (waterLevel == 0) {
			underFlow = true;
			plc3.processSensorData(0);
		}else if (waterLevel == 1) {
			low = true;
			plc3.processSensorData(1);
		}else if (waterLevel == 2) {
			medium = true;
			plc3.processSensorData(2);
		}else if (waterLevel == 3) {
			high = true;
			plc3.processSensorData(3);
		}else if (waterLevel == 4) {
			overFlow = true;
			plc3.processSensorData(4);
		}	
	}

}

reactiveclass PLC3(5){
	knownrebecs{
		Pump2 pump2;
		Tank3 tank3;
		sensorTank3 sensor;
	}
	statevars{
		boolean onReqPlc2;
	}
	PLC3(){	 
		onReqPlc2 = false;
	}
	msgsrv processSensorData(int waterLevel){
		if (waterLevel == 0) { 
			//plcAlarm = true;
		}else if (waterLevel == 1) { 
			pump2.off();
		}else if ((waterLevel == 2 && onReqPlc2 ) || (waterLevel == 3 && onReqPlc2)){
			onReqPlc2 == false;
			pump2.on();
		}else if (waterLevel == 3) {
			tank3.waterWholeDeplete();
			pump2.off();
		}else if (waterLevel == 2) {
			pump2.off();	
		}		
	}
	msgsrv onReq(){
		onReqPlc2 = true;
		sensor.sense();
	}

}

reactiveclass Pump2(5){
	knownrebecs{
		Tank2 tank2;
		Tank3 tank3;
	}
	statevars{
		boolean on;
	}
	Pump2(){
	 	on = false;	 	
	}
	msgsrv on(){	
		on = true;
		tank3.waterDecrease();
		delay(1);
		tank2.waterIncrease();
		
	}
	msgsrv off(){
		on = false;	
	}

}



//--------------------------Attacker------------------------------------
// It injects fake messages and commands in the wireless channel between one plc and one sensor/actuator (valve or pump)
reactiveclass Attacker(3){
	knownrebecs{
		PLC1	plc1;
		PLC2	plc2;
		PLC3	plc3;
		Pump1	pump1;
		Pump2	pump2;
		Valve	valve;
	}
	statevars{
		int msg; 
		int attackTime; 
	}
	Attacker(int chl, int maliciousMsg, int time){
		msg = maliciousMsg;
		attackTime = time;
	 	if (chl == 1) { //chl is channel number, chl = 1 (channelPlc1S), chl = 2 (channelPlc1P1), chl = 3 (channelPlc1V), chl = 4 (channelPlc2S), chl = 5 (channelPlc2P3), chl = 6 (channelPlc3S)
	 		self.channelPlc1S(msg, attackTime);
	 	}else if (chl == 2) {
	 		self.channelPlc1P1(msg, attackTime);
	 	}else if (chl == 3) {
	 		self.channelPlc1V(msg, attackTime);
	 	}else if (chl == 4) {
	 		self.channelPlc2S(msg, attackTime);
	 	}else if (chl == 5) {
	 		self.channelPlc3P3(msg, attackTime);
	 	}else if (chl == 6) {
	 		self.channelPlc3S(msg, attackTime);
	 	}	 	
	}
	msgsrv channelPlc1S(int v, int k){	//status = the value between 1 to 3 for the sensor//pointTime = the value between 0 to 3
		plc1.processSensorData(v) after(k);	
	}
	msgsrv channelPlc1P1(int v, int k){ //status = the value between 0 (close) to 1 (open) //pointTime = the value between 0 to 3
		if(v == 1) {
			pump1.on() after(k);
		}else if(v == 0) {
			pump1.off() after(k);	
		}
	}
	msgsrv channelPlc1V(int v, int k){	//status = the value between 0 (close) to 1 (open) //pointTime = the value between 0 to 3
		if(v == 1) {
			valve.open() after(k);
		}else if(v == 0) {
			valve.close() after(k);	
		}
	}
	msgsrv channelPlc2S(int v, int k){	
		plc2.processSensorData(v) after(k);	
	}
	msgsrv channelPlc3P3(int v, int k){	
		if(v == 1) {
			pump2.on() after(k);
		}else if(v == 0) {
			pump2.off() after(k);	
		}
	}
	msgsrv channelPlc3S(int v, int k){	//status = the value between 0 to 3 for the sensor//pointTime = the value between 0 to 3
		plc3.processSensorData(v) after(k);	
	}
}


main{
	PLC1 plc1(pump1, valve, sensor1):(); 				
	PLC2 plc2(plc1, plc3, sensor2):();
	PLC3 plc3(pump2, tank3, sensor3):();
	Tank1 tank1(sensor1):();				
	Tank2 tank2(sensor2, unit):();
	Tank3 tank3(sensor3, tank2):();
	sensorTank1 sensor1(tank1, plc1):();
	sensorTank2 sensor2(tank2, plc2):();	
	sensorTank3 sensor3(tank3, plc3):();
	Pump1 pump1(tank1):();
	Pump2 pump2(tank2, tank3):();
	Valve valve(tank1, tank2):();						
	reverseUnit unit(tank2, tank3):();
	Attacker attacker1(plc1, plc2, plc3, pump1, pump2, valve):(4,2,2); //(chl, status, Time) 
	Attacker attacker2(plc1, plc2, plc3, pump1, pump2, valve):(4,2,2); 
}
